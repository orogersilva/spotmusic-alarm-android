apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {

    compileSdkVersion buildConfig.compileSdk

    defaultConfig {

        applicationId "com.orogersilva.spotmusicalarm"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        debug {

            storeFile rootProject.file('keystore/debug.jks')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {

            storeFile rootProject.file('keystore/release.jks')
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    flavorDimensions "environment"

    productFlavors {

        dev {

            dimension "environment"

            applicationIdSuffix ".dev"
        }

        prod {

            dimension "environment"
        }
    }


    buildTypes {

        debug {

            buildConfigField "String", "SPOTIFY_CLIENT_ID",
                    "\"ff63264c74f94c07852357c4d4977473\""
            buildConfigField "String", "SPOTIFY_REDIRECT_URI",
                    "\"http://www.orogersilva.com/callback/\""

            signingConfig signingConfigs.debug
        }

        release {

            buildConfigField "String", "SPOTIFY_CLIENT_ID",
                    "\"${envOrEmpty('SPOTIFY_CLIENT_ID')}\""
            buildConfigField "String", "SPOTIFY_REDIRECT_URI",
                    "\"${envOrEmpty('SPOTIFY_REDIRECT_URI')}\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    sourceSets.androidTest.java.srcDirs += 'src/androidTest/kotlin'
    sourceSets.main.java.srcDirs += 'src/main/kotlin'
    sourceSets.test.java.srcDirs += 'src/test/kotlin'
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.android.support.appCompat
    implementation deps.android.support.constraintLayout

    implementation deps.kotlin.stdlib.jre

    implementation deps.spotify.auth
    implementation deps.spotify.player

    testImplementation deps.kotlin.test.jdk

    androidTestImplementation deps.android.test.espresso
    androidTestImplementation deps.android.test.runner
}

repositories {

    jcenter()

    flatDir {
        dirs 'libs'
    }
}

def envOrEmpty(String name) {
    return System.getenv(name) ? getProperty(name) : ''
}