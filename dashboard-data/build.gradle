apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion buildConfig.compileSdk

    defaultConfig {

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "int", "DASHBOARD_DATABASE_VERSION", '1'
        buildConfigField "String", "DASHBOARD_DATABASE_NAME", "\"dashboard.db\""

        buildConfigField "String", "SPOTIFY_API_URL",
                "\"https://api.spotify.com/v1/\""

        kapt {

            arguments {

                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.androidTest.java.srcDirs += 'src/androidTest/kotlin'
    sourceSets.main.java.srcDirs += 'src/main/kotlin'
    sourceSets.test.java.srcDirs += 'src/test/kotlin'

    testOptions {

        unitTests.all {

            testLogging {
                events 'passed', 'skipped', 'failed'
            }
        }
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {

    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"

    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {

    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':base')
    api project(':dashboard-domain')

    implementation deps.kotlin.stdlib.jdk

    implementation deps.android.arch.paging
    kapt deps.android.arch.room.compiler
    implementation deps.android.arch.room.runtime
    implementation deps.android.arch.room.rxjava
    kapt deps.dagger.compiler
    implementation deps.dagger.core
    ktlint deps.ktlint.core
    implementation deps.okhttp.core
    implementation deps.retrofit.adapter
    implementation deps.retrofit.core
    implementation deps.retrofit.gsonconverter
    implementation deps.rx.android
    implementation deps.rx.java

    kaptTest deps.dagger.compiler
    testImplementation project(':test-utils')
    testImplementation deps.android.arch.room.test
    testImplementation deps.kotlin.test.jdk
    testImplementation deps.mockitokotlin.core
    testImplementation deps.okhttp.test.mockwebserver

    androidTestImplementation deps.android.arch.core.test
    androidTestImplementation deps.android.test.runner
    androidTestImplementation deps.hamcrest.core
}